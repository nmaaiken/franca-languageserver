plugins  {
	id 'java'
	id 'application'
	id 'eclipse'
}
repositories {
	mavenCentral()
}

dependencies {
	
	compile group: 'org.franca', name: 'org.franca.core.dsl', version: '0.13.1'
	compile group: 'org.franca', name: 'org.franca.core.dsl.ide', version: '0.13.1'
	compile group: 'org.franca', name: 'org.franca.core', version: '0.13.1'
	compile "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"

}

// Add a start script for the socket case
mainClassName = 'org.franca.example.lsp.RunServer'
startScripts {
	applicationName = 'franca-socket'
}

// Add another start script for the standalone / self-contained case
task standaloneStartScript(type: CreateStartScripts) {
	mainClassName = 'org.franca.example.lsp.ServerLauncher'
	applicationName = 'franca-standalone'
	outputDir = new File(project.buildDir, 'scripts')
	classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
	from(standaloneStartScript)
	fileMode = 0755
}